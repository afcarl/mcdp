# test connected
mcdp {
    
    mcdp simple_cell = catalogue {

        provides voltage [set-of(V)]
        provides capacity [J]

        requires cost [$]
        requires mass [kg]

        model1 | {1.5 V} | 1 J |  5 $ | 0.20 kg 
        model2 | {1.5 V} | 1 J | 15 $ | 0.10 kg 
        model3 | {5.0 V} | 1 J |  5 $ | 0.30 kg

    }

    mcdp cell_plus_converter =  mcdp {
        provides voltage [set-of(V)]
        provides capacity [J]
        requires cost [$]
        requires mass [kg]

        sub converter = instance catalogue {
            provides voltage_out [set-of(V)]
            requires voltage_in  [set-of(V)]
            requires cost [$]
            requires mass [g]
    
            step_up1 |{5  V}      | {1.5 V} | 5 $  | 20 g  
            step_up2 |{12 V}      | {1.5 V} | 10 $ | 20 g  
            step_up2 |{12 V, 5 V} | {1.5 V} | 10 $ | 20 g  
        }

        sub cell = instance simple_cell

        voltage <= converter.voltage_out
        converter.voltage_in <= cell.voltage
        mass >= cell.mass + converter.mass
        cost >= cell.cost + converter.cost
        capacity <= cell.capacity
    }

    sub battery = instance choose(s: simple_cell, c: cell_plus_converter)
    requires cost, mass for battery
    provides voltage, capacity using battery
}