 # mcdp-plot --cache --plots ndp_graph_enclosed -D ../.. -d . racecar1

mcdp {
    jetson = instance `NVIDIAJetson
    servo = instance `Traxxas_2075
    tmotor = instance `Traxxas_3351
    hokuyo = instance `HokuyoURG10LX
    hub = instance `USBHub

    motor_battery = instance `Traxxas_2923X
    other_battery = instance `Energizer_XP8000AW
    charger = instance `TraxxasCharger
    adapter = instance `TraxxasAdapter
    structure_io_sensor = instance `StructureIOSensor
    traxxas_chassis = instance `TraxxasChassis
    vesc = instance `VESC
    USBMicroCharger = instance `USBMicroCharging
    power_strip = instance `Amazon_PowerStrip
    

    provides motion using traxxas_chassis
    cr required by traxxas_chassis ≤ cr provided by tmotor
    angular_placement required by traxxas_chassis ≤ angular_placement provided by servo

    ppm provided by vesc ≥ ppm required by servo
    pwm provided by vesc ≥ pwm required by tmotor

    usb required by vesc ≤ usb_out provided by jetson
    usb_in required by jetson ≤ usb provided by hokuyo
 
    in required by hub ≤ out1 provided by other_battery
    splitter = instance mcdp {
      requires in [`PortableDCPower]
      provides out1 [`PortableDCPower]
      provides out2 [`PortableDCPower]
    }
    in required by splitter ≤ out2 provided by other_battery

    in required by hokuyo ≤ out1 provided by splitter
    in required by jetson ≤ out2 provided by splitter

    in required by vesc ≤ out provided by motor_battery

    in required by motor_battery ≤ out provided by charger
    in required by charger ≤ out provided by adapter 
    in required by other_battery ≤ out provided by USBMicroCharger


    requires charging [`AC_Charging]

    in required by adapter ≤ out1 provided by power_strip
    in required by USBMicroCharger ≤ out2 provided by power_strip

    required charging ≥ in required by power_strip
    ignore out3 provided by power_strip
    ignore out4 provided by power_strip
    ignore out5 provided by power_strip
    ignore out6 provided by power_strip
    
    
    fit = instance template mcdp {
      provides p1 [`Payload]
      provides p2 [`Payload]
      provides p3 [`Payload]
      provides p4 [`Payload]
      provides p5 [`Payload]
      provides p6 [`Payload]
      provides p7 [`Payload]
      provides p8 [`Payload]
      provides p9 [`Payload]
      requires payload [`Payload]
  }
 
  p1 provided by fit ≥ payload required by jetson
  p2 provided by fit ≥ payload required by servo
  p3 provided by fit ≥ payload required by tmotor
  p4 provided by fit ≥ payload required by hokuyo
  p5 provided by fit ≥ payload required by hub
  p6 provided by fit ≥ payload required by motor_battery
  p7 provided by fit ≥ payload required by structure_io_sensor
  p8 provided by fit ≥ payload required by other_battery
  p9 provided by fit ≥ payload required by vesc
  payload required by fit ≤ payload provided by traxxas_chassis

    requires budget [USD]

    required budget ≥ (
        budget required by jetson + 
        budget required by servo + 
        budget required by tmotor + 
        budget required by hokuyo + 
        budget required by hub + 
        budget required by motor_battery + 
        budget required by other_battery + 
        budget required by charger + 
        budget required by adapter + 
        budget required by structure_io_sensor + 
        budget required by traxxas_chassis +
        budget required by vesc +
        budget required by USBMicroCharger +
        budget required by power_strip
    )
}
