mcdp {  
    # We need to fly for this duration
    provides endurance [s] 
    # While carrying this payload
    # (in addition to battery)
    provides extra_payload [kg]
    # And providing this extra power
    provides extra_power [W]

    # Sub-design problem: choose the battery
    battery = instance mcdp {
        # A battery provides capacity
        provides capacity [J]        
        # and requires some mass to be transported
        requires mass [kg] 
        # requires cost [$]

        specific_energy_Li_Ion = 500 Wh / kg 

        required mass >= provided capacity / specific_energy_Li_Ion
    }

    # Sub-design problem: actuation
    actuation = instance mcdp {
        # actuators need to provide this lift
        provides lift [N]
        # and will require power
        requires power [W]
        # simple model: quadratic
        c = 0.002 W/N^2
        l = provided lift
        required power >= l * l * c
    }
    # Co-design constraint: battery must be large enough
    power = power required by actuation+ provided extra_power
    energy = power * provided endurance
    battery.capacity >= energy

    # Co-design constraint: actuators must be powerful enough
    gravity = 9.81 m/s^2
    weight = (battery.mass + provided extra_payload) * gravity
    lift provided by actuation >= weight

    # we want to optimize for size of the battery
    requires mass for battery
}